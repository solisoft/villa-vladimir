# Controllers

Controllers handle HTTP requests and return responses. SoliLang supports OOP-style controllers with class inheritance, before/after action hooks, and automatic request context injection.

## Creating a Controller

Create a file in `app/controllers/` with a `_controller.sl` suffix:

```soli
# app/controllers/users_controller.sl
class UsersController < Controller
  def index
    @user = this._current_user
    @posts = Post.all
    render("posts/index")
  end

  def show
    # @post is set by before_action
    render("posts/show")
  end

  def show
    @user = User.find(params["id"])
    @title = "User Details"
    render("users/show")
  end
end
```

> **`req` is implicit.** The request hash is automatically available as a global `req` variable — you don't need to declare it as a parameter. When you do need to destructure the request (e.g. `req.params`), just reference it directly. The explicit `def index` form still works for backward compatibility.

> **No imports needed for models.** Files under `app/models/` are auto-loaded by `soli serve` and the REPL, so classes like `User`, `Post`, etc. are available inside controller actions without `import`. (If you run a controller file standalone via `soli run`, add the imports back.) The linter warns about redundant imports via `style/redundant-model-import`.

## OOP Controller Architecture

### Class-Based Controllers

Controllers are classes that extend the base `Controller` class. Actions take no explicit parameters — `req` is available automatically:

```soli
class PostsController < Controller
  # Actions go here
  def index end
  def show end
end
```

### Static Configuration Block

Configure controllers using a `static { ... }` block. The `static { ... }` block and the hook function bodies require brace syntax (the controller registry parses them textually):

```soli
class ApplicationController < Controller
  static {
    # Set the layout for all actions
    this.layout = "application";

    # Before action that runs for all actions
    this.before_action = fn(req) {
      user_id = req.session["user_id"];
      if user_id != null {
        req["current_user"] = User.find(user_id);
      }
      req
    }
  }
end
```

### Controller Actions

Each public function in a controller is an action:

```soli
class PostsController < Controller
  def index end
  def show end
  def new end
  def create end
  def edit end
  def update end
  def delete end
```

**Note:** Helpers are not actions. Methods starting with `_` get no automatic route, and a method under `private` or `protected` (or declared `private def`) is never an action: a route that names one answers 404, and it can only be called on `self` (`@helper(...)` or the bare `helper(...)`). See [Private Methods](soli-language.md#private-methods).

## Controller Inheritance

Controllers support multi-level inheritance. Create base controllers to share logic, hooks, and layouts across multiple controllers.

### Base Controller Pattern

Create an `ApplicationController` with shared configuration:

```soli
# app/controllers/application_controller.sl
class ApplicationController < Controller
  static {
    this.layout = "application";

    # Run for all actions
    this.before_action = fn(req) {
      # Authentication check
      user_id = req.session["user_id"];
      if user_id == null {
        return redirect("/login");
      }
      req["current_user"] = User.find(user_id);
      req
    }
  }

  # Shared helper method available to all subclasses
  def _current_user
    req["current_user"]
  end
end
```

Subclasses inherit the configuration and can override it:

```soli
# app/controllers/posts_controller.sl
class PostsController < ApplicationController
  static {
    # Override layout for this controller
    this.layout = "posts";

    # Run before_action only for specific actions
    this.before_action(:show, :edit, :update, :delete) = fn(req) {
      @post = Post.find(params["id"])    # raises 404 if not found
      req
    }
  }

  def index
    @user = this._current_user
    @posts = Post.all
  end

  def show
    # @post is set by before_action — template renders posts/show
  end
end
```

### Multi-level Inheritance

You can create deeper hierarchies. Each level inherits hooks and methods from its parent:

```soli
# app/controllers/admin_controller.sl
# Extends ApplicationController, which extends Controller
class AdminController < ApplicationController
  static {
    this.layout = "admin";

    this.before_action = fn(req) {
      # Parent's before_action already ran (authentication)
      halt(403, "Forbidden") if req["current_user"]["role"] != "admin"
      req
    }
  }
end

# app/controllers/admin_users_controller.sl
class AdminUsersController < AdminController
  def index
    # Inherits: ApplicationController's auth + AdminController's admin check
    @users = User.all
  end
end
```

### Inheritance Rules

- **Methods**: Inherited and can be overridden. Use `super.method()` to call the parent version.
- **before_action / after_action**: Inherited from parent controllers. Child hooks run after parent hooks.
- **layout**: Inherited if the child doesn't set its own.
- **Fields**: Declared in parent classes are available in child instances.
- **Loading order**: Parent controllers are automatically loaded before children (files are sorted by dependency).

## Before/After Action Hooks

### Before Actions

Run code before an action executes. Can filter to specific actions:

```soli
class PostsController < Controller
  static {
    # Run for all actions
    this.before_action = fn(req) {
      println("Before any action: " + req.path);
      req
    }

    # Run only for specific actions
    this.before_action(:show, :edit, :delete) = fn(req) {
      @post = Post.find(params["id"])    # raises 404 if not found
      req
    }
  }
end
```

**Short-circuiting:** Return a response hash (with a `"status"` field) from a before action to skip the action — or raise: `halt(...)`, `forbidden()` and a `Model.find` miss stop the hook wherever they are called, no `return` needed:

```soli
this.before_action = fn(req) {
  return redirect("/login") if session_get("user_id").nil?

  halt(403, "Forbidden") unless current_user().admin?   # raises: no `return` needed
  req  # Continue to action
}
```

A `forbidden()` or a `Model.find` miss raised inside a before_action (or a middleware) answers 403 / 404, exactly as it does inside an action.

### After Actions

Run code after an action executes:

```soli
class PostsController < Controller
  static {
    this.after_action = fn(req, response) {
      # Log the action
      println("Completed: " + req.path);
      response  # Return modified or original response
    }
  }
end
```

Filter after actions to specific actions:

```soli
this.after_action(:create, :update) = fn(req, response) {
  # Log changes after create/update
  println("Data modified");
  response
}
```

## Request Object

Access request data through the `req` parameter:

```soli
def create
  # Path parameters
  id = params["id"];

  # Query string parameters
  page = req.query["page"];

  # Form data
  name = req.form["name"];

  # JSON body (if Content-Type is application/json)
  data = req.json;

  # HTTP headers
  auth = req.headers["Authorization"];

  # HTTP method
  method = req.method;

  # Original path
  path = req.path;

  # Session data
  user_id = req.session["user_id"];

  # Parsed cookies (from Cookie header)
  session_id = req.cookies["session_id"];

  # Same value via the global shorthand
  session_id = cookies.session_id;

  # Actual TCP peer IP (no port). Used by `rate_limit` for buckets.
  # Honored as the trustworthy client identifier when `enable_trust_proxy()`
  # is off; otherwise the rightmost `X-Forwarded-For` entry wins.
  client_ip = req["remote_addr"];

  # Store data for after_action or views
  req["my_data"] = some_value;
end
```

### Request Context in Controllers

The request object is automatically injected into your controller:

```soli
class PostsController < Controller
  def show
    # `params` and `req` are in scope in every action
    @post = Post.find(params["id"])
  end
end
```

## Cookies

The `cookies` global gives you read access to cookies sent by the client. It is a hash parsed from the `Cookie` header, defaulting to `{}` when no cookies are present:

```soli
def show
  # Read a cookie
  theme = cookies["theme"] or "light";

  # Dot access also works
  session_id = cookies.session_id;
end
```

### set_cookie(name, value, options?)

Write a response cookie. The cookie is sent back to the client as a `Set-Cookie` header:

```soli
def login
  set_cookie("session_id", "abc123");
  set_cookie("theme", "dark", {"max_age": 86400, "http_only": true, "secure": true, "same_site": "Lax"});

  {"status": 200, "body": "Logged in"}
end
```

The options hash controls the cookie attributes: `path` (default `"/"`), `max_age` (seconds; `0` expires the cookie immediately), `expires` (RFC-1123 date), `http_only`, `secure`, `same_site` (`"Lax"`/`"Strict"`/`"None"`) and `domain` — plus the `signed`/`encrypted` sealing options below. Unknown keys raise, so a typo can't silently weaken a cookie.

`Secure` is added for you in two cases beyond asking for it: when `same_site` is `"None"` (browsers drop such a cookie without it, so the alternative is one that silently never arrives), and when the operator has set `SOLI_FORCE_SECURE_COOKIES=1` / called `enable_force_secure_cookies()` — that switch covers every cookie the process emits, not just the framework's session cookie. Set `"secure": true` explicitly anyway on anything that authenticates: a deployment that forgot the switch shouldn't be what decides whether a long-lived credential travels in the clear.

Cookies set via `set_cookie` are visible in templates and subsequent reads within the same request through the `cookies` global; `read_cookie` sees them too.

### Signed and encrypted cookies

Bare cookies are attacker-writable: anything the client sends in the `Cookie` header lands in `cookies` verbatim. When a cookie carries a value you need to *trust* — or one the client shouldn't be able to read — seal it with the `signed` or `encrypted` option and read it back with `read_cookie`:

```soli
def remember_theme
  # Encrypted: opaque on the client, accepts any JSON-serializable value.
  set_cookie("prefs", {"theme": "dark", "cols": [1, 2]}, {"encrypted": true, "max_age": 86400});

  # Signed: readable on the client but tamper-proof.
  set_cookie("uid", 42, {"signed": true});
end

def show
  prefs = read_cookie("prefs", {"encrypted": true});   # {"theme": "dark", "cols": [1, 2]}
  uid = read_cookie("uid", {"signed": true});          # 42
  raw = read_cookie("theme");                          # plain read, like cookies["theme"]
end
```

The reader states the trust requirement: a bare cookie named `uid` that an attacker sets to `42` reads as `nil` through `read_cookie("uid", {"signed": true})` — only a value your server sealed verifies. Tampered, expired, forged or mode-mismatched values all read as `nil`, indistinguishable from an absent cookie.

How it works:

- Encrypted cookies are AES-256-GCM sealed (`enc.v1.` prefix); signed cookies are HMAC-SHA256 authenticated with the payload readable as base64url JSON (`sig.v1.` prefix).
- Both keys are HKDF-derived from `SOLI_SESSION_SECRET` (32+ characters, same secret as the `cookie` session driver — rotating it invalidates all sealed cookies). Sealing without a secret raises; set the env var or call `session_configure({"secret": ...})`.
- The cookie **name** is bound into the seal, so a validly-sealed value copied from one cookie into another reads as `nil`.
- A `max_age` option is also embedded as an expiry *inside* the sealed payload — a captured cookie can't be replayed past its intended lifetime even if the client ignores the browser-level `Max-Age`.
- `signed` and `encrypted` are mutually exclusive; the sealed value counts against the ~4KB cookie limit and oversize raises at write time.

## Returning Responses

### Render a Template

```soli
def index
  render("home/index", {
    "title": "Welcome",
    "message": "Hello!"
  })
end
```

### Bare view names resolve to the controller's directory

Inside an action, `render("new")` means this controller's own view: from `PostsController` it renders `posts/new`. That is the same directory the automatic render uses (`posts#index` → `posts/index`), so re-rendering a form after a failed save reads the way it does in Rails:

```soli
class PostsController < Controller
  def create(req)
    @post = Post.create(permit(params, {"title": true}))
    return render("new", {}, {"status": 422}) if @post._errors

    redirect(post_path(@post))
  end
end
```

The controller's directory wins only when that file exists, so `render("about")` still finds a top-level `about.html.slv` when `posts/about` does not exist. A name with a `/` is always taken as written (`render("users/show")`, `render("shared/form")`), and so is any name outside a controller action (a job, a mailer, a helper). When neither file exists, the error names both: `Template 'about' not found in app/views (looked for 'posts/about' first)`.

### Instance Fields Auto-Exposed to Views

Any field set on the controller instance during an action — via either `this.foo = ...` or the `@foo` shorthand — is automatically available as a bare local in the view that action renders. You can drop the data hash entirely when you just want to pass data through.

```soli
class PostsController < Controller
  def show
    @post = Post.find(params["id"]);
    @comments = Comment.where({"post_id": @post.id}).all;
    render("posts/show")    # view sees `post` and `comments` with no data hash
  end
end
```

In the view you can reference these either as a bare local or with the **same `@` prefix you used in the controller** — in views, `@post` falls back to the `post` local, so both forms render the same value:

```erb
<%# app/views/posts/show.html.erb %>
<h1><%= @post.title %></h1>   <%# @-form, mirrors the controller %>
<%= post.body %>              <%# bare local, identical result %>

<h2>Comments (<%= @comments.length %>)</h2>
<% for c in comments %>
  <p><%= c.body %></p>
<% end %>
```

A view's `@foo` that has no matching local renders as empty (`nil`), the same as any other absent template local.

Rules:

- **Explicit render data wins.** `render("v", {"post": other})` overrides `@post`.
- **Framework fields are never re-exposed** via this path: `req`, `params`, `session`, `headers` always flow through their normal channels, so an action can't accidentally shadow them.
- **Scoped to the current action.** No cross-action, cross-controller, or cross-request leakage — a fresh controller instance is created per request.
- **Partials are not auto-exposed.** Always pass data to `render_partial(...)` / `partial(...)` explicitly. Inside the partial, read keys as bare identifiers (`<%= name %>`) or via the `locals` hash (`<%= locals["class"] %>`) — see [Views → The `locals` hash](./views.md#the-locals-hash).

> **Note on `@foo`:** `@foo` is a general language shorthand for `this.foo` inside any class method, not a controller-only feature. See [Soli Language → The `@` Sigil](./soli-language.md#the--sigil--shorthand-for-this) for the full rules.

### Request-Context Helpers in Views

These helpers read the current request directly — no need to plumb `current_path` or `current_method` through the data hash:

| Helper | Returns |
|--------|---------|
| `current_path()` | Request pathname, e.g. `"/users"`. `null` when called outside a request. |
| `current_method()` | HTTP method, e.g. `"GET"`. `null` outside a request. |
| `current_path?(p)` | `true` if the current path equals `p` exactly. Handy for active-link checks. |

```erb
<%# app/views/layouts/_nav.html.erb %>
<nav>
  <a href="/users" class="<%= current_path?("/users") ? "active" : "" %>">Users</a>
  <a href="/posts" class="<%= current_path?("/posts") ? "active" : "" %>">Posts</a>
</nav>

<p>You are viewing <%= current_path() %> (<%= current_method() %>).</p>
```

For prefix matches (e.g. any path under `/users`), compose with string methods: `current_path().starts_with("/users")`.

### Render with Custom Layout

Set the layout in your controller:

```soli
class PostsController < Controller
  static {
    this.layout = "posts";  # Uses layouts/posts.html.slv
  }

  def show
    # @post is available from before_action
  end

  # Skip layout for specific action
  def json_only
    render_json({ "data": "value" }, layout: false)
  end
end
```

### Per-Action Layouts

A single controller can serve different layouts to different actions — declared
once in the `static { ... }` block, so you never have to repeat `layout:` on
each `render(...)` call. `this.layout = "..."` sets the controller-wide default;
`this.layout("name", only: [...])` / `except: [...]` override it for specific
actions:

```soli
class ReportsController < Controller
  static {
    this.layout = "admin";                              # default for every action

    this.layout("print", only: [:invoice, :receipt]);  # these two use "print"
    this.layout("blank", except: [:index]);            # everything else but :index uses "blank"
  }

  def invoice
    render("reports/invoice")   # → "print" layout, no `layout:` needed
  end

  def index
    render("reports/index")     # → "admin" (excluded from "blank", not in "print")
  end
end
```

Resolution rules:

- Rules are checked **in declaration order**; the **first match wins**, then the
  controller-wide `this.layout` default, then the framework `"application"`
  layout.
- `only:` limits a rule to the listed actions; `except:` applies it to every
  action *but* those listed. Omit both and the rule applies to all actions
  (equivalent to setting the default).
- An explicit `layout:` passed to `render(...)` (including `layout: false` to
  skip layouts) always wins over any registered rule.
- Per-action rules are **inherited** by subclasses just like the default
  layout; a child's own rule for the same action overrides the inherited one.

Edits to these declarations are picked up on the next request in `--dev` mode —
no server restart required.

### Redirect

```soli
def create
  # Process form data...

  # Redirect to another page
  redirect("/users")
end

def update
  # After update, redirect to show page
  user_id = params["id"];
  redirect("/users/" + user_id)
end
```

`redirect()` only accepts local absolute paths such as `/login` or `/users/123`. This prevents accidentally turning user-controlled input into an open redirect.

For trusted external destinations, use `redirect_external()` explicitly:

```soli
def oauth_start
  redirect_external("https://github.com/login/oauth/authorize")
end
```

To send the user back where they came from, pass the `:back` symbol. Soli reads the `Referer` header and only honors it when scheme + host match the current request — external referers (or a missing/malformed header) fall back to `/`.

```soli
def destroy
  Comment.find(params["id"]).delete()
  redirect(:back)
end
```

### JSON Response

```soli
def api_users
  render_json({
    "users": [
      {"id": 1, "name": "Alice"},
      {"id": 2, "name": "Bob"}
    ]
  })
end
```

> **Security — instance serialisation.** `render_json(instance)` (and any code path that JSON-stringifies a `Value::Instance`, including `to_json` on a Model record) **omits sensitive fields by default**. Names matching `password*`, `*_token`, `*_digest`, `*_secret`, or `*_hash` are dropped, as are `_`-prefixed framework internals (`_errors`, `_text`, `_pending_translations`, …). The standard Model metadata (`_key`, `_id`, `_rev`, `_created_at`, `_updated_at`) is still included. If you need to expose a field whose name matches one of the patterns, build the response shape explicitly: `render_json({ "id": user._key, "email": user.email, "auth_token_count": user.auth_token_count })` instead of `render_json(user)`.

For a reusable model-side shape, define an `as_json` method on the Model subclass:
>
> ```soli
> class User < Model
>   def as_json
>     return { "id": this._key, "email": this.email, "name": this.name }
>   end
> end
>
> # controller — render_json auto-dispatches through the user method:
> render_json(user)
> # equivalent to: render_json(user.as_json())
> ```
>
> Same convention as Rails' `ActiveModel::Serializers#as_json`. When `render_json` receives an `Instance` whose class declares `def as_json`, the framework calls the method first and forwards the resulting Hash to `render_json`. Models without an `as_json` method fall back to the default-deny filter described above. Defining `as_json` gives you a single declarative place to evolve a model's public API shape.

### JSONP Response

`render_jsonp(data, status?)` serves a JSONP response for legacy cross-origin
consumers. It reads the callback function name from the request's `?callback`
query param and wraps the serialized JSON as `/**/callback({...});` with
`Content-Type: application/javascript`. The same default-deny filter and `as_json`
convention as `render_json` apply to the serialized `data`.

```soli
def feed
  render_jsonp({ "items": [1, 2, 3] })
end
```

- `GET /feed?callback=handleFeed` → `/**/handleFeed({"items":[1,2,3]});` as `application/javascript`.
- `GET /feed` (no `callback`) → falls back to a plain JSON response (`application/json`).
- `GET /feed?callback=alert(1)` → `400 Bad Request`; the callback name is **not** reflected.

The callback name is validated against a strict identifier whitelist (letters,
digits, `_`, `$`, and dots, ≤64 chars — allowing names like
`angular.callbacks._0`) before it is ever written into the body, which closes the
classic JSONP XSS vector.

> **Security — JSONP is cross-origin readable.** A JSONP endpoint can be loaded
> by any site via a `<script>` tag, bypassing the same-origin policy. This is why
> it is a dedicated opt-in helper rather than an automatic mode of `render_json`.
> Only expose data through `render_jsonp` that is safe to read from any origin —
> never authenticated, per-user, or otherwise sensitive data.

### Plain Text

```soli
def ping
  render_text("pong")
end
```

### Content Negotiation with `respond_to`

For actions that need to serve multiple formats (HTML, JSON, CSV, PDF, XLSX, partial HTMX, XHR-only JSON, …), use `respond_to`. It picks the right branch based on the request and falls back to `406 Not Acceptable` when no registered format matches.

```soli
def show
  @post = Post.find(params["id"])
  respond_to(req, fn(format) {
    format.html(fn()  render("posts/show"))
    format.json(fn()  render_json(@post))
    format.csv(fn()   render_csv_for(@post))
    format.pdf(fn()   render_pdf_for(@post))
    format.excel(fn() render_xlsx_for(@post))
    format.htmx(fn()  render("posts/_show_partial", {}, { "layout": false }))
    format.xhr(fn()   render_json({ "id": @post.id }))
    format.eui(fn()   eui_render(post_view(@post)))   # an EUI client
    format.any(fn()   render("posts/show"))           # optional catch-all
  })
end
```

The `eui` branch answers `application/vnd.eui.frames` — an EUI client asking
for the interface already resolved rather than for markup to build it from. A
page and its EUI form are two representations of one thing, so they share a
route, a URL and its params; see [EUI: serving a page with no session](eui/overview.md#serving-a-page-with-no-session).
The match is on that exact media type and never on a substring, because a
browser's `Accept` ends in `*/*` and a looser rule would hand every browser a
screenful of binary.

A terser hash form is also supported:

```soli
respond_to(req, {
  "html": fn() render("posts/show"),
  "json": fn() render_json(@post)
})
```

**Format detection priority** (first match wins):

1. `HX-Request: true` header → `htmx` branch.
2. `X-Requested-With: XMLHttpRequest` header → `xhr` branch.
3. URL extension: `.html`, `.json`, `.xml`, `.csv`, `.pdf`, `.xlsx`/`.xls`, `.txt`, `.eui`.
4. `?format=…` query parameter.
5. `Accept` header — parsed with q-values; `*/*` falls through to the first registered handler.

**Available format tokens**: `html`, `json`, `xml`, `csv`, `pdf`, `excel`, `htmx`, `xhr`, `text`, `any`. Registering `any` makes it the catch-all (no 406). Last registration wins on duplicates.

> Header keys in `req["headers"]` are lowercased — read `req["headers"]["accept"]`, not `Accept`.

### Error Response

`halt(status, message)` stops the request and answers `status` with `message` as a `text/plain` body. Like Sinatra's `halt`, it **raises**: a bare `halt(...)` ends the action right there — and works the same from a `before_action`, a middleware, or a helper several calls deep, without threading a return value back up.

```soli
def show
  halt(400, "Missing ID") if params["id"].blank?

  @user = find_user(params["id"])
  halt(404, "User not found") if @user.nil?
end

private

def _require_owner(post)
  halt(403, "Not your post") unless post.author_id == current_user().id
end
```

- `status` must be an `Int` between 100 and 599; anything else raises an ordinary error (500).
- `return halt(...)` still works — the raise happens before the `return`.
- `try`/`catch` and postfix `rescue` catch it like any other raise (the caught value is the message), so a halt inside a `try` block does not reach the client unless you re-raise.

> Before, `halt` *returned* a response hash: only `return halt(...)` stopped anything, and a bare `halt(...)` statement fell through while the action carried on.

## Controller Context

Controllers have access to context through `this`:

```soli
class PostsController < Controller
  static {
    this.layout = "posts";
    this.before_action = fn(req) {
      # Store data on request for later use
      req["post"] = Post.find(params["id"]);
      req
    }
  }

  def show
    # Access the post set by before_action
    @post = req["post"]
  end

  # Access request parameters
  def _get_id -> String
    params["id"]
  end
end
```

## Strong Parameters

Validate and sanitize input:

```soli
def create
  params = req.form;
  clean_params = {
    "name": params["name"] ?? "",
    "email": params["email"] ?? "",
    "age": int(params["age"] ?? "0")
  };
end
```

## Routing to Controller Actions

Routes use `controller#action` syntax:

```soli
# config/routes.sl
get("/", "home#index");
get("/users", "users#index");
get("/users/:id", "users#show");
post("/users", "users#create");
```

The router automatically:
1. Instantiates a new controller instance per request
2. Injects the request context
3. Runs before_action hooks
4. Calls the action method
5. Runs after_action hooks
6. Returns the response

## File Naming Convention

| File | Class | Route Prefix |
|------|-------|--------------|
| `home_controller.sl` | `HomeController` | `home#` |
| `users_controller.sl` | `UsersController` | `users#` |
| `posts_controller.sl` | `PostsController` | `posts#` |
| `admin/users_controller.sl` | `AdminUsersController` | `admin/users#` |
| `admin/merchants_controller.sl` | `AdminMerchantsController` | `admin/merchants#` |

## Nested Controller Directories

Controllers can be organized into subdirectories under `app/controllers/`. The directory path becomes part of the controller key, the route base path, and the class name.

```
app/controllers/
├── home_controller.sl              # HomeController            → /
├── users_controller.sl             # UsersController           → /users
└── admin/
    ├── merchants_controller.sl     # AdminMerchantsController  → /admin/merchants
    └── user_profiles_controller.sl # AdminUserProfilesController → /admin/user_profiles
```

Both `_` and `/` act as word separators when deriving the class name, so `admin/user_profiles_controller.sl` becomes `AdminUserProfilesController` (not `Admin::UserProfilesController`).

Reference nested controllers from `config/routes.sl` using the same `controller#action` syntax with a `/`-separated key:

```soli
get("/admin/merchants", "admin/merchants#index");
get("/admin/merchants/:id", "admin/merchants#show");

# Or with resources()
resources("/admin/merchants", "admin/merchants");
```

Subdirectories are watched recursively in dev mode, so adding or editing a nested controller triggers hot reload like any top-level controller.

## Best Practices

1. **Keep controllers thin, models fat** - Business logic belongs in models
2. **Use before_action for authentication** - Common pattern for access control
3. **Validate parameters before processing** - Use strong parameters pattern
4. **Return appropriate HTTP status codes** - 200, 201, 400, 401, 404, 500
5. **Use redirects after successful POST requests** - Prevent form resubmission
6. **Use private helper methods** - Put helpers under a `private` section (or prefix them with `_`): they are never exposed as actions
7. **Create ApplicationController** - Base class for shared configuration
8. **Use layouts consistently** - Set default layout in ApplicationController

## Testing Controllers

See the [Testing Guide](/docs/testing) for comprehensive information on testing controllers with both HTTP integration tests and direct action calls.
